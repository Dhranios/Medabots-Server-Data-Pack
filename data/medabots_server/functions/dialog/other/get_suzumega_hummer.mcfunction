execute if entity @s[scores={EditingTinpet=0..}] run function medabots_server:items/tinpet/stop_editing
scoreboard players operation @s[scores={Dialog=0}] DialogNr > @a[scores={DialogNr=0..}] DialogNr
scoreboard players add @s[scores={Dialog=0}] DialogNr 1
execute store result score #temp DialogNr run scoreboard players get @s DialogNr
execute as @e[tag=cutscene] if score @s DialogNr = #temp DialogNr run tag @s add this_dialog
scoreboard players add @s Dialog 1
tag @s[scores={Dialog=1}] add get_suzumega_hummer_dialog
advancement grant @s[scores={Dialog=1}] only medabots_server:story_progression/other get_suzumega_hummer
execute if entity @s[scores={Dialog=1}] run summon minecraft:area_effect_cloud ^ ^ ^3 {Duration:1000000,Tags:["cutscene","suzumega_hummer","medabot","this_dialog"]}
execute if entity @s[scores={Dialog=1}] positioned ^ ^ ^3 run scoreboard players operation @e[distance=..1,tag=suzumega_hummer] MedabotNr > @e[scores={MedabotNr=0..}] MedabotNr
execute if entity @s[scores={Dialog=1}] positioned ^ ^ ^3 run scoreboard players add @e[distance=..1,tag=suzumega_hummer] MedabotNr 1
execute if entity @s[scores={Dialog=1}] run summon minecraft:armor_stand ^ ^ ^3 {NoGravity:1b,Invisible:1b,Marker:1b,Tags:["cutscene","suzumega_hummer","legs","medabot_model","model_piece","found_owner","found_owner_2"],CustomName:'{"translate":"medabots_server:entity.model_piece"}',ArmorItems:[{},{},{},{id:"minecraft:fishing_rod",Count:1b,tag:{Damage:1,Unbreakable:1b,HideFlags:4,CustomModelData:67,display:{Name:'{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_execute"}',Lore:['{"italic":false,"color":"white","translate":"medabots_server:move.fly"}','{"italic":false,"color":"white","translate":"medabots_server:move.fly.description"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.defense","with":[{"text":"20"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.armor","with":[{"text":"30"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.male"}','{"italic":false,"color":"white","translate":"medabots_server:entity.suzumega_hummer","color":"green"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.wave","with":[{"text":"1"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_execute.model"}']},medabots_server:{stage_item:0b,id:"medabots_server:suzumega_hummer_execute",gender:0b,move:"fly",part:"legs",armor:30,defense:20,activated:0b,model_data:{height:0,leg_offset:[]},version:1}}}]}
execute if entity @s[scores={Dialog=1}] run summon minecraft:armor_stand ^ ^ ^3 {NoGravity:1b,Invisible:1b,Marker:1b,Tags:["cutscene","suzumega_hummer","left_arm","medabot_model","model_piece","found_owner","found_owner_2"],CustomName:'{"translate":"medabots_server:entity.model_piece"}',ArmorItems:[{},{},{},{id:"minecraft:fishing_rod",Count:1b,tag:{Damage:2,Unbreakable:1b,HideFlags:4,CustomModelData:67,display:{Name:'{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_light_hammer"}',Lore:['{"italic":false,"color":"white","translate":"medabots_server:move.hammer"}','{"italic":false,"color":"white","translate":"medabots_server:move.hammer.description"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.uses.infinite"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.armor","with":[{"text":"12"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.male"}','{"italic":false,"color":"white","translate":"medabots_server:entity.suzumega_hummer","color":"green"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.wave","with":[{"text":"1"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_light_hammer.model"}']},medabots_server:{stage_item:0b,id:"medabots_server:suzumega_hummer_light_hammer",gender:0b,move:"hammer",part:"left_arm",armor:12,power:70,activated:0b,version:1}}}]}
execute if entity @s[scores={Dialog=1}] run summon minecraft:armor_stand ^ ^ ^3 {NoGravity:1b,Invisible:1b,Marker:1b,Tags:["cutscene","suzumega_hummer","right_arm","medabot_model","model_piece","found_owner","found_owner_2"],CustomName:'{"translate":"medabots_server:entity.model_piece"}',ArmorItems:[{},{},{},{id:"minecraft:fishing_rod",Count:1b,tag:{Damage:3,Unbreakable:1b,HideFlags:4,CustomModelData:67,display:{Name:'{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_chopper_sword"}',Lore:['{"italic":false,"color":"white","translate":"medabots_server:move.sword"}','{"italic":false,"color":"white","translate":"medabots_server:move.sword.description"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.uses.infinite"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.armor","with":[{"text":"12"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.male"}','{"italic":false,"color":"white","translate":"medabots_server:entity.suzumega_hummer","color":"green"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.wave","with":[{"text":"1"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_chopper_sword.model"}']},medabots_server:{stage_item:0b,id:"medabots_server:suzumega_hummer_chopper_sword",gender:0b,move:"sword",part:"right_arm",armor:12,power:65,activated:0b,version:1}}}]}
execute if entity @s[scores={Dialog=1}] run summon minecraft:armor_stand ^ ^ ^3 {NoGravity:1b,Invisible:1b,Marker:1b,Tags:["cutscene","suzumega_hummer","head","medabot_model","model_piece","found_owner","found_owner_2"],CustomName:'{"translate":"medabots_server:entity.model_piece"}',ArmorItems:[{},{},{},{id:"minecraft:fishing_rod",Count:1b,tag:{Damage:4,Unbreakable:1b,HideFlags:4,CustomModelData:67,display:{Name:'{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_datalink"}',Lore:['{"italic":false,"color":"white","translate":"medabots_server:move.scout"}','{"italic":false,"color":"white","translate":"medabots_server:move.scout.description"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.uses.more","with":[{"text":"7"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.armor","with":[{"text":"40"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.male"}','{"italic":false,"color":"white","translate":"medabots_server:entity.suzumega_hummer","color":"green"}','{"italic":false,"color":"white","translate":"medabots_server:item.medapart.wave","with":[{"text":"1"}]}','{"italic":false,"color":"white","translate":"medabots_server:item.suzumega_hummer_datalink.model"}']},medabots_server:{stage_item:0b,id:"medabots_server:suzumega_hummer_datalink",gender:0b,move:"scout",part:"head",armor:40,power:94,uses:7,activated:0b,version:1}}}]}
execute if entity @s[scores={Dialog=1}] positioned ^ ^ ^3 as @e[tag=medabot_model,distance=..1] run scoreboard players operation @s MedabotNr = @e[distance=..1,limit=1,tag=suzumega_hummer] MedabotNr
execute if entity @s[scores={Dialog=1}] positioned ^ ^ ^3 as @e[tag=suzumega_hummer,distance=..1] run scoreboard players operation @s DialogNr = #temp DialogNr
execute if entity @s[scores={Dialog=1..375}] at @e[tag=suzumega_hummer,tag=this_dialog,limit=1] facing entity @s feet run teleport @e[distance=..0.1,tag=suzumega_hummer,tag=this_dialog,limit=1] ~ ~ ~ ~ ~
tellraw @s[scores={Dialog=1}] {"translate":"chat.type.text","with":[{"translate":"medabots_server:entity.suzumega_hummer","color":"green"},{"translate":"medabots_server:dialog.other.get_suzumega_hummer.1"}]}
tellraw @s[scores={Dialog=104}] {"translate":"chat.type.text","with":[{"translate":"medabots_server:entity.suzumega_hummer","color":"green"},{"translate":"medabots_server:dialog.other.get_suzumega_hummer.2"}]}
tellraw @s[scores={Dialog=152}] {"translate":"chat.type.text","with":[{"translate":"medabots_server:entity.suzumega_hummer","color":"green"},{"translate":"medabots_server:dialog.other.get_suzumega_hummer.3"}]}
tellraw @s[scores={Dialog=272}] {"translate":"chat.type.text","with":[{"translate":"medabots_server:entity.suzumega_hummer","color":"green"},{"translate":"medabots_server:dialog.other.get_suzumega_hummer.4"}]}
execute if entity @s[scores={Dialog=352},tag=!already_gave_items] run function medabots_server:give_items/medaparts/suzumega_hummer
tag @s[scores={Dialog=352}] add already_gave_items
tellraw @s[scores={Dialog=372}] {"translate":"chat.type.text","with":[{"translate":"medabots_server:entity.suzumega_hummer","color":"green"},{"translate":"medabots_server:dialog.other.get_suzumega_hummer.5"}]}
tellraw @s[scores={Dialog=436}] {"translate":"chat.type.text","with":[{"translate":"medabots_server:entity.suzumega_hummer","color":"green"},{"translate":"medabots_server:dialog.other.get_suzumega_hummer.6"}]}
execute if entity @s[scores={Dialog=476}] as @e[tag=suzumega_hummer,tag=this_dialog,limit=1] at @s run teleport @s ~ ~ ~ ~ -90
execute if entity @s[scores={Dialog=477..}] as @e[tag=suzumega_hummer,tag=this_dialog,limit=1] at @s run teleport @s ~ ~0.2 ~
tag @s[scores={Dialog=497}] remove get_suzumega_hummer_dialog
tag @s[scores={Dialog=497}] remove dialog_other_get_suzumega_hummer
tag @s[scores={Dialog=497}] remove already_gave_items
scoreboard players reset @s[scores={Dialog=497}] DialogNr
scoreboard players set @s[scores={Dialog=497}] Dialog 0
scoreboard players reset #temp DialogNr
tag @e[tag=this_dialog] remove this_dialog